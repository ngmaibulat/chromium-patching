# Copyright 2025 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

template("generate_code_cache") {
  action(target_name) {
    script = "//ui/webui/resources/tools/generate_code_cache.py"

    forward_variables_from(invoker,
                           [
                             "deps",
                             "in_folder",
                             "in_files",
                             "out_folder",
                             "out_manifest",
                           ])
    deps += [ "//tools/code_cache_generator" ]

    inputs = []
    outputs = [ out_manifest ]
    foreach(f, in_files) {
      extension = get_path_info(f, "extension")
      assert(extension == "js")
      inputs += [ "${in_folder}/${f}" ]
      outputs += [ "${out_folder}/${f}.code_cache" ]
    }

    args = [
             "--util_path",
             "./code_cache_generator",
             "--in_folder",
             rebase_path(in_folder, root_build_dir),
             "--out_folder",
             rebase_path(out_folder, root_build_dir),
             "--out_manifest",
             rebase_path(out_manifest, root_build_dir),
             "--in_files",
           ] + in_files
  }
}
